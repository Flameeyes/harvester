<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0"
  xmlns="http://www.w3.org/1999/xhtml"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:hv="http://astroblog.spaceboyz.net/harvester/xslt-functions"
  exclude-result-prefixes="xsl hv">

<xsl:output method="xml"
            version="1.0"
            encoding="utf-8"
            doctype-public="-//W3C//DTD XHTML 1.0 Strict//EN"
            doctype-system="DTD/xhtml1-strict.dtd"
            indent="yes"/>


<xsl:template match="/collections">
  <html xmlns="http://www.w3.org/1999/xhtml" lang="de">
    <head>
      <title>Blog Harvester</title>
      <link rel="alternate" type="application/rss+xml" title="RSS 1.0" href="rss.rdf" />
      <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
      <link rel="stylesheet" type="text/css" href="harvester.css"/>
    </head>

    <body>
      <h1>Blog Harvester</h1>
      <div class="tickers1">
        <div class="ticker">
          <h2>Harvesting</h2>
          <ul class="tickerlinks">
            <xsl:for-each select="collection[@name='blogs']/feed">
              <xsl:sort select="translate(title, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz')"/>
              <li><a href="{link}" title="{description}"><xsl:value-of select="title"/></a></li>
            </xsl:for-each>
          </ul>
        </div>
        <div class="ticker">
          <h2>Apropos</h2>
          <p>
            The Harvester is made of fresh flesh from the blogosphere,
            clean design by Tigion
            and hacked-together Ruby scripts by Astro,
            improved by Neingeist and Josef.
          </p>
        </div>
        <div class="ticker">
          <h2>To be fed</h2>
          <p>
            Subscribe to <a href="atom.xml">ATOM 1.0</a> or <a href="rss.rdf">RSS 2.0</a>,
            but be aware that none of these will ever completely validate.
          </p>
        </div>
        <div class="ticker">
          <h2>Be notified</h2>
          <p>Start Jabber and talk to <a href="xmpp:astrobot@spaceboyz.net">AstroBot</a> to receive the freshest of all!</p>
        </div>
        <div class="ticker">
          <h2>Questions</h2>
          <p>Missing something?<br/>Contact <a href="mailto:astro@spaceboyz.net">Astro</a> via Mail or Jabber: astro@spaceboyz.net</p>
        </div>
        <div class="ticker">
          <h2>Hack it...</h2>
          <p>
            <a href="https://wiki.c3d2.de/Harvester">Project info</a>,
            <a href="svn://svn.c3d2.de/harvester/trunk/harvester">Repository</a>,
            <a href="http://trac.c3d2.de/harvester">Trac</a>
          </p>
          </div>
        <div class="ticker">
          <h2>...or learn more</h2>
          <ul class="tickerlist">
            <li><a href="../harvester-0.6.tar.bz2">harvester-0.6.tar.bz2</a> (14148 Bytes, <a href="../harvester-0.6.tar.bz2.sig">Sig</a>)</li>
            <li><a href="../harvester-0.5.tar.bz2">harvester-0.5.tar.bz2</a> (16101 Bytes, <a href="../harvester-0.5.tar.bz2.sig">Sig</a>)</li>
            <li><a href="../harvester-0.4.tar.bz2">harvester-0.4.tar.bz2</a> (9731 Bytes, <a href="../harvester-0.4.tar.bz2.sig">Sig</a>)</li>
            <li><a href="../harvester-0.3.2.tar.bz2">harvester-0.3.2.tar.bz2</a> (8663 Bytes, <a href="../harvester-0.3.2.tar.bz2.sig">Sig</a>)</li>
            <li><a href="../harvester-0.3.1.tar.bz2">harvester-0.3.1.tar.bz2</a> (8519 Bytes, <a href="../harvester-0.3.1.tar.bz2.sig">Sig</a>)</li>
            <li><a href="../harvester-0.3.tar.bz2">harvester-0.3.tar.bz2</a> (8411 Bytes, <a href="../harvester-0.3.tar.bz2.sig">Sig</a>)</li>
            <li><a href="../harvester-0.2.tar.bz2">harvester-0.2.tar.bz2</a> (4184 Bytes, <a href="../harvester-0.2.tar.bz2.sig">Sig</a>)</li>
          </ul>
          </div>

          <xsl:call-template name="ticker-boxen">
            <xsl:with-param name="collection" select="'ticker'"/>
          </xsl:call-template>
        </div>

        <div class="tickers2">
          <xsl:call-template name="ticker-boxen">
            <xsl:with-param name="collection" select="'links'"/>
          </xsl:call-template>
        </div>

        <xsl:variable name="blogs" select="collection[@name='blogs']"/>
        <xsl:for-each select="hv:collection-items('blogs')/item">
          <xsl:variable name="rss" select="string(rss)"/>
          <xsl:variable name="feed" select="$blogs/feed[rss=$rss]"/>
          <div class="entry">
            <h2>
              <a href="{$feed/link}"><xsl:value-of select="$feed/title"/></a>
            </h2>
            <div class="entrydate"><xsl:value-of select="date"/></div>
            <h3><a href="{link}"><xsl:value-of select="title"/></a></h3>
            <div class="entrydesc">
              <xsl:value-of select="hv:item-description($rss, string(link))" disable-output-escaping="yes"/>
            </div>
            <div class="entryfoot"><a href="{link}" title="{link}">mehr...</a></div>
          </div>
       </xsl:for-each>
    </body>
  </html>
</xsl:template>

<xsl:template name="ticker-boxen">
  <xsl:param name="collection"/>

  <xsl:for-each select="collection[@name=$collection]/feed">
    <xsl:sort select="translate(title, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 'abcdefghijklmnopqrstuvwxyz')"/>
    <div class="ticker">
      <h2><a href="{link}"><xsl:value-of select="title"/></a></h2>
      <ul class="tickerlinks">
        <xsl:for-each select="hv:feed-items(string(rss), 8)/item">
          <li>
            <xsl:choose>
              <xsl:when test="string-length(title) &gt; 25">
                <a href="{link}" title="{title}"><xsl:value-of select="substring(title, 1, 20)"/>...</a>
              </xsl:when>
              <xsl:otherwise>
                <a href="{link}"><xsl:value-of select="title"/></a>
              </xsl:otherwise>
            </xsl:choose>
          </li>
        </xsl:for-each>
      </ul>
    </div>
  </xsl:for-each>
</xsl:template>

</xsl:stylesheet>
